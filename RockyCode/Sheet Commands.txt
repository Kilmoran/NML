Sheet Commands


Command Object (General): #60
Staff Command Object: #1223
Sheet Function Object: #596

&CMD`SHEET #60=$+sheet:@pemit %#=[printf($^78:[ansi(hb,-=)]:s, %[ [ansi(hw, %k's Sheet)] %])]%r[printf($^24s $^29s $^24s,Clan: [get(%#/dat`clan)],Ninja Tier: [u(#2082/func`shinobirank)],Stat Tier: [get(%#/dat`tier)])]%r[repeat([ansi(hb,-)],78)]%r[u(#594/fn`stats,%#)]%r[u(#594/fn`potential, %#)]%r[u(#594/fn`attacks, %#)]%r[u(#594/fn`defenses, %#)]%r[u(#594/fn`transforms, %#)]%r[u(#594/fn`rpskills, %#)]%r[repeat([ansi(hb,-=)],39)]

&CMD`+SHEET-BRIEF #60=$^\+sheet/(brief|stats|attacks|defenses|defend|attack|transforms|tfs|pot|potential|rpskills|rp)$:@pemit %#={[setq(x, [switch(%1, brief, stats, attack, attacks, defend, defenses, tfs, transforms, rp, rpskills, pot, potential, %1)])][printf($^78:[ansi(hb,-=)]:s, %[ [ansi(hw, %k's Sheet)] %])]%r[printf($^24s $^27s $^24s,Clan: [get(%#/dat`clan)], Ninja Tier: [u(#2082/func`shinobirank)],Stat Tier: [get(%#/dat`tier)])]%r[repeat([ansi(hb, -)], 78)]%r[u(#594/fn`%qx,%#)]%r[repeat([ansi(hb,-=)],39)]}

&CMD`SHARE-SHEET #60=$+sheet/show *:@break [t([setr(0,[pmatch(%0)])])]={@pemit %#=[prompt(game)] You have offered to show [ansi(y, [name(%q0)])] your sheet.;@pemit %q0=[prompt(game)] [ansi(c, %N)] wants to show you %p sheet. Type [ansi(hg, +sheet/accept)] or [ansi(hg, +sheet/decline)] to respond.;&dat`sheetshower %q0=%#}; @pemit %#=[prompt(Error)] Invalid Player

&CMD`SHEET-RESPONSE #60=$^\+sheet/(accept|decline)$:@break [hasattr(%#, dat`sheetshower)]={@break [strmatch(%1, accept)]={@eval [setq(0, [get(%#/dat`sheetshower)])]; @pemit %#={[printf($^78:[ansi(hb,-=)]:s, %[ [ansi(hw, [cname(%q0)]'s Sheet)] %])]%r[printf($^24s $^29s $^24s,Clan: [get(%q0/dat`clan)], Ninja Rating: [u(#594/func`shinobirank, %q0)],Stat Tier: [get(%q0/dat`tier)])]%r[repeat([ansi(hb,-)],78)]%r[u(#594/fn`stats,%q0)]%r[u(#594/fn`potential, %q0)]%r[u(#594/fn`attacks, %q0)]%r[u(#594/fn`defenses, %q0)]%r[u(#594/fn`transforms, %q0)]%r[u(#594/fn`RPskills, %q0)]%r[repeat([ansi(hb,-=)],39)]};@pemit %q0=[prompt(game)] You have shown [ansi(y, [cname(%#)])] your sheet.;&dat`sheetshower %#=};@pemit %#=[prompt(game)] You have [ansi(r, declined)] to see [ansi(y, [name([get(%#/dat`sheetshower)])])]'s sheet.;@pemit [get(%#/dat`sheetshower)]=[prompt(game)] [ansi(y, %N)] has [ansi(r, declined)] to see your sheet.;&dat`sheetshower %#=};@pemit %#=[prompt(game)] No one wants to show you their sheet.

&CMD`MBSSHEET #60=$+MBSSheet: @assert [strmatch([get(%#/dat`clan)], Yamanaka)]={@pemit %#=[prompt(Game)] You are not permitted to use this command.}; @assert [and([hasattr(%#, dat`exposure)], [elements([get(%#/dat`exposure)], 3, |)])]={@pemit %#=[prompt(game)] You do not have anyone under your Control.}; @eval [setq(0, [elements([get(%#/dat`exposure)], 1, |)])]; @pemit %#=[printf($^78:[ansi(hb,-=)]:s, %[ [ansi(hw, [cname(%q0)]'s Sheet)] %])]%r[printf($^24s $^27s $^24s,Clan: [get(%q0/dat`clan)], Ninja Tier: [u(#2082/func`staffshinobirank,%0)],Stat Tier: [get(%q0/dat`tier)])]%r[repeat([ansi(hb,-)],78)]%r[u(#594/fn`stats,%q0)]%r[u(#594/fn`attacks, %q0)]%r[u(#594/fn`defenses, %q0)]%r[u(#594/fn`transforms, %q0)]%r[u(#594/fn`RPskills, %q0)]%r[repeat([ansi(hb,-=)],39)]

&CMD`STAFF-SHEET #1223=$+sheet *:@break [orslabs(%#,staff-cg staff-skill staff-plot)]={@break [t([setr(0,[pmatch(%0)])])]={@pemit %#=[printf($^78:[ansi(hb,-=)]:s, %[ [ansi(hw, [cname(%q0)]'s Sheet)] %])]%r[printf($^24s $^27s $^24s,Clan: [get(%q0/dat`clan)], Ninja Tier: [u(#2082/func`staffshinobirank,%0)],Stat Tier: [get(%q0/dat`tier)])]%r[repeat([ansi(hb,-)],78)]%r[u(#594/fn`stats,%q0)]%r[u(#594/fn`potential, %q0)]%r[u(#594/fn`attacks, %q0)]%r[u(#594/fn`defenses, %q0)]%r[u(#594/fn`transforms, %q0)]%r[u(#594/fn`RPskills, %q0)]%r[repeat([ansi(hb,-=)],39)]};@pemit %#=[prompt(Error)] Invalid Player};@pemit %#=[error()]

&CMD`+SHEET-BRIEF #1223=$^\+sheet/(brief|stats|attacks|defenses|defend|attack|transforms|tfs|pot|potential|rpskills|rp) (.*)$:@break [orslabs(%#, staff-CG staff-Plot)]={@break [t([setr(0, [pmatch(%2)])])]={@pemit %#={[setq(x, [switch(%1, brief, stats, attack, attacks, defend, defenses, tfs, transforms, rp, rpskills, pot, potential, %1)])][printf($^78:[ansi(hb,-=)]:s, %[ [ansi(hw, [cname(%q0)]'s Sheet)] %])]%r[printf($^24s $^27s $^24s,Clan: [get(%q0/dat`clan)], Ninja Tier: [u(#2082/func`staffshinobirank, %q0)],Stat Tier: [get(%q0/dat`tier)])]%r[repeat([ansi(hb, -)], 78)]%r[u(#594/fn`%qx,%q0)]%r[repeat([ansi(hb,-=)],39)]}}; @pemit %#=[prompt(game)] Please choose a valid player.}; @pemit %#=[prompt(game)] You are not allowed to do that.
@set #1223/CMD`+SHEET-BRIEF = regexp



@create Sheet Functions=10

@set Sheet Functions=INHERIT SIDEFX

&FN`STATS Sheet Functions=[printf($&10s $&-4s $&14s $&-4s $&14s $&-4s $&14s $&-4s,Taijutsu:%rSpeed:,[elements([get(%0/dat`primarystats)], 1, |)]%r[elements([get(%0/dat`primarystats)], 5, |)],Ninjutsu:%rStamina:,[elements([get(%0/dat`primarystats)], 2, |)]%r[elements([get(%0/dat`primarystats)], 6, |)],Genjutsu:%rIntelligence:,[elements([get(%0/dat`primarystats)], 3, |)]%r[elements([get(%0/dat`primarystats)], 7, |)],Power:%rHand Seals:,[elements([get(%0/dat`primarystats)], 4, |)]%r[elements([get(%0/dat`primarystats)], 8, |)])]%r%r[printf($10s $-13s $10s $-13s $10s $-13s,[ansi(hr, Vitality)]:,[ansi(hw,[get(%0/dat`vitality`current)])]|[ansi(hr,[get(%0/dat`vitality`Max)])],[ansi(hg, Stamina)]:,[ansi(hw,[get(%0/dat`stamina`current)])]|[ansi(hg,[get(%0/dat`stamina`Max)])],[ansi(hc, Chakra)]:,[ansi(hw,[get(%0/dat`chakra`current)])]|[ansi(hc,[get(%0/dat`chakra`max)])])]%r[printf($20s $-13s $20s $-13s,[ansi(hm, Physical Energy)]:,[get(%0/dat`physicalenergy`current)]/[get(%0/dat`physicalenergy`max)],[ansi(hm, Spiritual Energy)]:,[get(%0/dat`spiritualenergy`current)]/[get(%0/dat`spiritualenergy`max)])]%r[printf($#3#^78s,[ansi(hc,Focus Efficiency:)] [round([mul(mul(add(1,fdiv(add([extract(get(%0/Dat`PrimaryStats),7,1,|)],[extract(get(%0/Dat`TranscendAttribs),7,1,|)],[extract(get(%0/Dat`PrimaryStats),8,1,|)],[extract(get(%0/Dat`TranscendAttribs),8,1,|)]),10)),[if(match(get(%0/Dat`merits),Perfected Chakra Molding,|),1.25,[if(match(get(%0/Dat`Flaws),Erratic Chakra,|),.75,1)])]),100)],0)][chr(37)] %t [ansi(hy,Action Points:)] [ansi(hw,[get(%0/dat`ap`current)])]|[ansi(hy,[get(%0/dat`ap`max)])])]

&FN`ATTACKS Sheet Functions=[printf($^78:[ansi(hb, -)]:s, %[ Attacks %])]%r[iter([setunion(,[parse([edit(lattr(%0/dat`jutsuattributes`*), DAT`JUTSUATTRIBUTES`,)], [if([strmatch(Attack, [sqlite_query(Game, select Method from Templates where ID='[setr(1, [elements([get(%0/dat`jutsuattributes`##)], 7,|)], id)]')])], [match([v(list`ranks)], [sqlite_query(Game, select Rank from Templates where ID='%q<id>')])]:##)])])], [printf($3s $22-s,[elementsmux([get(%0/dat`jutsuattributes`[after(##, :)])], 14, |)][elements([v(list`ranks)], [before(##, :)])]:, [ansi([switch([sqlite_query(Game, select Category from Templates where ID='[elements([get(%0/dat`jutsuattributes`[after(##, :)])], 7,|)]')], Genjutsu, hm, Ninjutsu, hc, Taijutsu, y)], [elements([get(%0/dat`jutsuattributes`[after(##, :)])], 8,|)])])][if([mod(#@, 3)],,%r)],,)]

&FN`DEFENSES Sheet Functions=[printf($^78:[ansi(hb, -)]:s, %[ Defenses %])]%r[iter([setunion(,[parse([edit(lattr(%0/dat`jutsuattributes`*), DAT`JUTSUATTRIBUTES`,)], [if([strmatch(Defend, [sqlite_query(Game, select Method from Templates where ID='[setr(1, [elements([get(%0/dat`jutsuattributes`##)], 7,|)], id)]')])], [match([v(list`ranks)], [sqlite_query(Game, select Rank from Templates where ID='%q<id>')])]:##)])])], [printf($3s $22-s,[elementsmux([get(%0/dat`jutsuattributes`[after(##, :)])], 14, |)][elements([v(list`ranks)], [before(##, :)])]:, [ansi([switch([sqlite_query(Game, select Category from Templates where ID='[elements([get(%0/dat`jutsuattributes`[after(##, :)])], 7,|)]')], Genjutsu, hm, Ninjutsu, hc, Taijutsu, y)], [elements([get(%0/dat`jutsuattributes`[after(##, :)])], 8,|)])])][if([mod(#@, 3)],,%r)],,)]

&FN`RPSKILLS Sheet Functions=[printf($^78:[ansi(hb, -)]:s, %[ RP Skills %])]%r[columns([iter(u(#244/dat`rpskills), if(elements(u(%0/dat`rpskills), #@,|), ## %([elements(u(%0/dat`rpskills), #@,|)]%)|),|,)], 19, 4, l, 0, 1,,,, 1, |)]

&FN`POTENTIAL Sheet Functions=[printf($:[ansi(hb,-)]:^78s,%[ Potential %])]%r[printf($&^25s $&^25s $&^25s,[ansi(hyu,Merits)]%r[iter([get(%0/dat`merits)],[caplist(##)],|,%r)],[ansi(hyu,Flaws)]%r[iter([get(%0/dat`flaws)],[caplist(##)],|,%r)],[ansi(hyu,Affinities)]%r[iter([get(%0/dat`affinitylist)],[caplist(##)],|,%r)])]

&FN`TRANSFORMS Sheet Functions=[printf($^78:[ansi(hb, -)]:s, %[ Transformations %])]%r[printf($&^36s $&^4s $&^36s,[if([hasattr(%0/dat`tflist)],[ansi(uh,Transformations)]%r[iter([get(%0/dat`tflist)],##,|,%r)],)],%r,[if([hasattr(%0/dat`tclist)],[ansi(uh,Transcendences)]%r[iter([get(%0/dat`tclist)],##,|,%r)],)])]

&LIST`RANKS Sheet Functions=S A B C D E

&FUNC`SHINOBIRANK Sheet Functions=[setq(r, [ceil(ladd([fdiv(ladd(extract(get(%0/Dat`RPSkills),1,1),|),10)]|[fdiv(ladd(extract(get(%0/Dat`PrimaryStats),1,1),|),2)]|[words([get(%0/dat`ExpertiseList)], |)],|))])][switch(1, [lte(%qr, 10)], E, [lte(%qr, 20)], D, [lte(%qr, 30)], C, [lte(%qr, 40)], B, [lte(%qr, 50)], A, [lte(%qr, 70)], S, Legendary)] Shinobi


