@create XP System=10

@set XP System=INHERIT SIDEFX MONITOR

&FN`RECORD-POSE XP System=[set(%L, count:[mod([inc([u(%l/count)])], 10)])][set(%L, pose`[u(%L/count)]:%N:%0)][set(#2220, dat`[timefmt($b, [secs()])]:[replace([get(#2220/dat`[timefmt($b, [secs()])])], [timefmt($D, [secs()])], [add([elements([get(#2220/dat`[timefmt($b, [secs()])])], [timefmt($D, [secs()])])], [strlen(%0)])])])][set(#2220, dat`[timefmt($b, [secs()])]-pCount:[replace([get(#2220/dat`[timefmt($b, [secs()])]-pCount)], [timefmt($D, [secs()])], [inc([elements([get(#2220/dat`[timefmt($b, [secs()])]-pCount)], [timefmt($D, [secs()])])])])])]

&LIST`XP-NAMED-MODS XP System=NewPlayer MiniString SmallString MediumString LargeString JumboString SmallScene MediumScene LargeScene PlotFlag MasterApprentice Rival Enemy Companion MoodSet

&LIST`XP-NUM-MODS XP System=7 4 7 11 15 19 7 9 13 8 5 6 7 6 5

&FN`XP-CLEAR XP System=[if([and([hasattr(%0, dat`nextclear)], [gte([secs()], [get(%0/dat`nextclear)])])], [iter([lcon(%0/connect)], [set(##, dat`xp-gauge:)][set(##, dat`nextclear:)])][set(%0, dat`nextclear:)][wipe(%0/mood)][wipe(%0/pose`*)][wipe(%0/count)])]

&FN`XP-GAINED-CALC XP System=[setq(0, [iter(setunion(lwho(),),[if([regmatchi([get(##/dat`xp-gauge)],%(mini|small|medium|large|jumbo%)string)], ##)])])][iter(%q0, [u(fn`update-xp, ##)])]

&FN`UPDATE-XP XP System=[if([!hastags(%0, watching)], [set(%0, dat`xp-gained:[add([get(%0/dat`xp-gained)],[ladd([elements([v(list`xp-num-mods)], [parse([u(%0/dat`xp-gauge)], [match([v(list`xp-named-mods)], ##)])])])])])])]

&FN`XP-PAYOUT XP System=[setq(0, [iter([search(type=player)], [if([hasattr(##, dat`xp-gained)], ##)])])][iter(%q0, [set(%i0, dat`xp:[add([get(%i0/dat`xp)],[get(%i0/dat`xp-gained)])])][Set(%i0,Dat`XP-weekly:[Add(get(%i0/Dat`xp-weekly),[get(%i0/dat`xp-gained)])])][Set(%i0,Dat`XP-lifetime:[Add(get(%i0/Dat`xp-lifetime),[get(%i0/dat`xp-gained)])])][wipe(%i0/dat`xp-gained)][if([gte([get(%i0/dat`XP-lifetime)], 10000)], [settag(%i0, !newplayer)])])]

&FN`PLOT-MOD XP System=[iter([setunion([parse([lwho()], ##:[hastags([loc(##)], plot)])],)], [if([after(##,:)], [set([before(##,:)], dat`xp-gauge:[setunion([get([before(##,:)]/dat`xp-gauge)], PlotFlag)])], [set([before(##, :)], dat`xp-gauge:[setdiff([get([before(##, :)]/dat`xp-gauge)], PlotFlag)])])])]

&TR`XP-MODS XP System=@trigger me/fn`plot-mod;@trigger me/fn`scene-size-mod;@trigger me/fn`teacher-mod;@trigger me/fn`rival-mod;@trigger me/fn`companion-mod;@trigger me/fn`newplayer-mod;@trigger me/fn`mood-mod

&FN`SCENE-SIZE-MOD XP System=[iter(setunion(lwho(),), [setq(0, words(iter(lcon(loc(##), connect), if(hastags(%i0, watching),,%i0))))][set(##, dat`xp-gauge:[setunion([switch(1, gt(%q0, 7), LargeScene, gt(%q0, 4), MediumScene, gt(%q0, 2), SmallScene)], [setdiff([get(##/dat`xp-gauge)], LargeScene SmallScene MediumScene)])])])]

&TR`XP-CLEAR XP System=@dolist/inline search(type=room)={@trigger #121/fn`xp-clear=##}

&FN`LP-GAIN XP System=[iter([sqlite_query(Game, select Email from Accounts)], sqlite_query(Game, update Accounts Set LP=[add([sqlite_query(Game, select LP from Accounts where Email='%i0')], [setr(+, [gte([ladd([iter([sqlite_query(Game, select Approved%, "Approved NPCs" from Accounts where Email='%i0',%b)], [default(%i0/dat`xp-weekly, 0)][set(%i0, dat`xp-weekly:0)])])], 500)], bool)])]%, "LP Total"=[add([sqlite_query(Game, select "LP Total" from Accounts where Email='%i0')], %q<bool>)] where Email="%i0"),^)]

&FN`TEACHER-MOD XP System=[iter([setunion([lwho()],)], [if([and([hasattr(##, dat`sensei)], [!hastags([setr(t, [get(##/dat`sensei)])], watching)], [t([conn(%qt)])])], [if([strmatch([loc(##)], [loc(%qt)])], [set(##, dat`XP-Gauge:[setunion([get(##/dat`XP-Gauge)], MasterApprentice)])][set(%qt, dat`XP-gauge:[setunion([get(%qt/dat`XP-Gauge)], MasterApprentice)])])], )])]

&FN`RIVAL-MOD XP System=[iter([setunion([lwho()],)], [if([and([!hastags([get(##/dat`rival)], watching)], [t([conn([get(##/dat`rival)])])], [strmatch([loc([get(##/dat`rival)])], [loc(##)])])], [set(##, dat`xp-gauge:[setunion([get(##/dat`xp-gauge)], Rival)])][set([get(##/dat`rival)], dat`xp-gauge:[setunion([get([get(##/dat`rival)]/dat`xp-gauge)], Rival)])])])]

&TR`WATCH-RESET XP System=@dolist/clearreg/inline [setunion([lwho()],)]={@eval [if([or([gte([idle(##)], 3600)], [lt([get(##/dat`nextclear)], [secs()])])], [set(##, dat`xp-gauge:)][set(%#, dat`nextclear:)][settag(##, watching)])]}

&FN`COMPANION-MOD XP System=[iter([setunion([lwho()],)], [if([and([!hastags([get(##/dat`companion)], watching)], [t([conn([get(##/dat`companion)])])], [strmatch([loc([get(##/dat`companion)])], [loc(##)])])], [set(##, dat`xp-gauge:[setunion([get(##/dat`xp-gauge)], Companion)])][set([get(##/dat`companion)], dat`xp-gauge:[setunion([get([get(##/dat`companion)]/dat`xp-gauge)], Companion)])])])]

&FN`NEWPLAYER-MOD XP System=[iter([setunion([parse([lwho()], ##:[t([lor([iter([lcon([loc(##)], connect)], [hastags(%i0, newplayer)])])])])],)], [if([after(##,:)], [set([before(##,:)], dat`xp-gauge:[setunion([get([before(##,:)]/dat`xp-gauge)], NewPlayer)])], [set([before(##, :)], dat`xp-gauge:[setdiff([get([before(##, :)]/dat`xp-gauge)], NewPlayer)])])])]

&FN`MOOD-MOD XP System=[iter([setunion([lwho()],)], [if([hasattr([loc(##)], mood)], [set(##, dat`xp-gauge:[setunion([get(##/dat`xp-gauge)], MoodSet)])])])]

&TR`XP-AVG-GAIN-CALC XP System=@eval [words([setr(0, searchng(eplayer=and%(lt%(bittype%(##%), 2%), gt%(get%(##/dat`xp-lifetime%), 0%)%)))])][iter(%q0, [setq(1, [add([get(##/dat`xp-lifetime)], %q1)])],,)];@set #121=dat`xp-gain-avg:[insert([v(dat`xp-gain-avg)], -1, %q1)]; @cemit Monitor-App=AVG: [div(%q1, [words(%q0)])] Lifetime XP gained per Character with More than 0 Lifetime XP.


XP System DBref: #121
Cron System DBref: #26


&CRON_JOB_XP-PAYOUT #26=@wait 0={@trigger #121/fn`xp-payout}

&CRON_TIME_XP-PAYOUT #26=|||08|00|

&CRON_JOB_XP-GAIN #26=@trigger #121/fn`xp-gained-calc

&CRON_TIME_XP-GAIN #26=||||00 06 12 18 24 30 36 42 48 54|

&CRON_JOB_XP-MODS #26=@wait 0={@trigger #121/tr`XP-Mods}

&CRON_TIME_XP-MODS #26=||||05 11 17 23 29 35 41 47 53 59|

&CRON_TIME_XP-CLEAR #26=||||00 01 02 03 04 05 06 07 08 09 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 49 50 51 52 53 54 55 56 57 58 59|

&CRON_JOB_XP-CLEAR #26=@trigger #121/tr`xp-clear

&CRON_JOB_XP-AVG-GAIN-CALC #26=@trigger #121/tr`xp-avg-gain-calc

&CRON_TIME_XP-AVG-GAIN-CALC #26=||Sunday|08|00|
