
Order Object: #1247
Order Functions: #590

@create Order System=10
@set Order System=INHERIT SIDEFX

&CMD`ORDER`JOIN #1247=$+joinrp:@assert [strmatch(CG Parent, [name([parent(%l)])])]={@assert [t([match([get(%l/list`players)], %#)])]={@eval [set(%#, dat`ap`current:[get(%#/dat`ap`max)])][set(%l, list`players:[insert([get(%l/list`players)], -1, %#)])][pemit(%#, [prompt(combat)] You have joined +order.)][oemit(%#, [prompt(combat)] [ansi(hc, %N)] has joined +order.)];@break [eq(1, [words([get(%l/list`players)])])]={@eval [set(%l, player_turn:1)][set(%l, pt_player:%#)][settag(%#, ordermaster)][remit(%l, [prompt(combat)] It is now [ansi(hc, %N)]'s turn.)]}};@pemit %#=[prompt(combat)] You are already in +order.};@pemit %#=[prompt(cg)] Please leave CG before you enter combat.

&CMD`ET #1247=$+et:@break [t([match([get(%l/list`players)], %#)])]={@break [strmatch(%#, [get(%l/pt_player)])]={@assert [hasattr(%l, attacks`%#)]={@eval [u(fn`next-select, [get(%l/list`players)], [match([get(%l/list`players)], %#)], %l)][set(%#, dat`ap`current:[get(%#/dat`ap`max)])][u(#590/fn`on-ET)];@pemit %#=[prompt(combat)] You have ended your turn.;@oemit %#=[prompt(combat)] [ansi(hc, %N)] finishes %p turn.;@remit %l=[prompt(combat)] It is now [ansi(hc, [name([get(%l/pt_player)])])]'s turn.; @eval [u(#253/func`defendprompt, [get(%l/pt_player)])]};@pemit %#=[prompt(combat)] You still have attacks against you.};@pemit %#=[prompt(combat)] It is not your turn.};@pemit %#=[prompt(combat)] You are not in +order.

&FN`NEXT-SELECT #1247=[if([t([setr(+, [rest([extract(%0, %1, 2)])], next)])], [set(%2, pt_player:%q<next>)][set(%2, player_turn:[match(%0, %q<next>)])], [set(%2, player_turn:1)][set(%2, pt_player:[elements(%0, 1)])])]

&CMD`LEAVE #1247=$+leaverp:@break [t([match([get(%l/list`players)], %#)])]={@break [and([neq(1, [words([get(%l/list`players)])])], [hastags(%#, ordermaster)])]={@pemit %#=[prompt(combat)] Please transfer your Order Master powers to someone else before leaving +order.}; @break [eq(1, [words([get(%l/list`players)])])]={@eval [set(%l, list`players:)][settag(%#, !ordermaster)][pemit(%#, [prompt(combat)] You have left +order.)][oemit(%#,[prompt(combat)] [ansi(hc, %N)] has left +order.)][remit(%l, [prompt(combat)] There is no one in +order.)][if([hasattr(%#, dat`copylist)], [iter([get(%#/dat`copylist)], [wipe(%#/dat`jutsu*[edit(##, %b, -)])], |)][wipe(%#/dat`copylist)])][if([hasattr(%#, Dat`TF)],[pemit(%#,[prompt(Game)] You have reverted from your [get(%#/Dat`TF)] Transformation.)][set(%#,Dat`TF:)])][iter(link exposure control, [if([hasattr(%#, dat`##)], [wipe(%#/dat`##)])])]};@skip [!strmatch(%#, [get(%l/pt_player)])]={@eval [u(fn`next-select, [get(%l/list`players)], [match([get(%l/list`players)], %#)], %l)]; @remit %l=[prompt(combat)] It is now [ansi(hc, [name([get(%l/pt_player)])])]'s turn.; @eval [u(#253/func`defendprompt, [get(%l/pt_player)])]}; @eval [set(%l, list`players:[remove([get(%l/list`players)], %#)])][set(%l, player_turn:[match([get(%l/list`players)], [get(%l/pt_player)])])][u(#590/fn`wipe-combat-attribs, %#)];@pemit %#=[prompt(combat)] You have left +order.;@oemit %#=[prompt(combat)] [ansi(hc, %N)] has left +order.}; @pemit %#=[prompt(combat)] You are not in +order.

&CMD`ORDER`BOOT #1247=$+kick *:@break [t([match([get(%l/list`players)], %#)])]={@break [t([setr(0, [locate(%#, %0, nP)])])]={@break [t([match([get(%l/list`players)], %q0)])]={@break [strmatch(%#, %q0)]={@pemit %#=[prompt(combat)] You cannot kick yourself from order. Please [ansi(hg, +leaverp)] instead.}; @break [or([t([conn(%q0)])], [gte([idle(%q0)], 600)])]={@wipe %l/attacks`%q0; @pemit %#=[prompt(combat)] You have kicked [ansi(y, [name(%q0)])] from +order.;@oemit %#=[prompt(combat)] [ansi(hc, [name(%q0)])] has been kicked from +order.; @skip [!strmatch(%q0, [get(%l/pt_player)])]={@eval [u(fn`next-select, [get(%l/list`players)], [match([get(%l/list`players)], %q0)], %l)]; @remit %l=[prompt(combat)] It is now [ansi(hc, [name([get(%l/pt_player)])])]'s turn.; @eval [u(#253/func`defendprompt, [get(%l/pt_player)])]}; @eval [set(%l, list`players:[remove([get(%l/list`players)], %q0)])][set(%l, player_turn:[match([get(%l/list`players)], [get(%l/pt_player)])])][u(#590/fn`wipe-combat-attribs, %q0)]; @skip/ifelse [hastags(%q0, ordermaster)]={@eval [settag(%q0, !ordermaster)][settag(%#, ordermaster)][pemit(%#, [prompt(game)] You have been granted Order Master %([ansi(hg, +order/help)]%) powers. Use them wisely.)]}}; @pemit %#=[prompt(combat)] That player hasn't been idle for longer than 10 minutes.}; @pemit %#=[prompt(combat)] That person isn't a member of +order.}; @pemit %#=[prompt(combat)] [ansi(y, %0)] is not a valid player.}; @pemit %#=[prompt(combat)] You must be a part of +order to +kick someone from it.

&CMD`ORDER`REORDER #1247=$+order/reorder *:@skip/ifelse [hastags(%#, ordermaster)]={@eval [setq(0, [iter(%0, [if([and([t([locate(%#, ##, nP)])], [match([get(%l/list`players)], [locate(%#, ##, nP)])])], [locate(%#, ##, nP)], [pemit(%#, [ansi(y, ##)] is not a player in the current +order.)])])])]; @break [eq([words([get(%l/list`players)])], [words([setinter([get(%l/list`players)], %q0)])])]={@eval [set(%l, list`players:[listunion(%q0,)])][set(%l, player_turn:[match(%q0, [get(%l/pt_player)])])]; @remit %l=[prompt(game)] The new +order is: [elist(%q0,,,,,[ansi([if([strmatch(%0, [get(%l/pt_player)])], m, n)], [name(%0)])])]}; @pemit %#=[prompt(game)] You do not have the correct players for a reorder.}, @pemit %#=[prompt(game)] You don't have permission to do that.

&CMD`ORDER`MASTER #1247=$+order/master *:@skip/ifelse [hastags(%#, ordermaster)]={@eval [switch(0, [t([setr(0, [pmatch(%0)])])], [pemit(%#, [prompt(game)] %0 is not a valid player.)], [!strmatch(%#, %q0)], [pemit(%#, [prompt(game)] You cannot give yourself Order Master powers.)], [member([get(%l/list`players)], %q0)], [pemit(%#, [prompt(game)] You must give the Order Master powers to a person in the scene!)], [set(%#, tags:[remove([get(%#/tags)], ordermaster)])][set(%q0, tags:[setunion([get(%q0/tags)], ordermaster)])][pemit(%#, [prompt(game)] You have given Order Master powers to [name(%q0)].)][pemit(%q0, [prompt(game)] You have been give Order Master powers. Use them wisely.)])]}, @pemit %#=[prompt(game)] You don't have permission to do that.

&CMD`+ATTACKS #1247=$+attacks:@pemit %#=[ansi(hwu, [printf($15-s | $15-s | $36-s, Defender, Attacker, Skill %(Roll%))])]%r[iter([lattr(%l/attacks`*)], [iter([get(%l/%i0)], [printf($15-s | $15-s | $46-s, [name([after(%i1, `)])],[name([elements(%i0, 1, :)])], [if([t([elements(%i0, 7, :)])], [elements(%i0, 7, :)]%b)][elements(%i0, 2,:)] %([elements(%i0, 4, :)]%))],^,%r)],,)]

@Desc #1247=[printf($78:[ansi(hb, -=)]:^s, < [ansi(hw, +order Help)] >)]%r[formathelp({[ansi(hy,General Commands)]%r* +order: Displays those currently in the order and their statuses. %r* +status: Displays a quickview of your current status. %r* +joinrp: Join the order to be able to participate in combat. %r* +leaverp: Leave the order. %r* +et: Ends your turn. %r* +kick <player>: Allows someone in the order to boot a player from the order after 10 minutes of idleness have passed. If the player kicked out is the Order Master, powers are assigned to the kicker.%r* +order/master <player>: Assigns <player> to be the Order Master. %r* +order/reorder <player player player etc.>: Allows the Order Master to reorder the players by listing them in the desired order.})]%r[printf($78:[ansi(hb, -=)]:^s, < [ansi(hw, End +order Help)] >)]

&CMD`HELP #1247=$+order/help:[pemit(%#, [u(desc)])]

&CMD`ORDER #1247=$+order:[if([eq([words([u(%l/list`players)])], 0)], [pemit(%#, [prompt(combat)] There is no one in the order.)],[pemit(%#, [prompt(RP)] [printf($-18s $-25s $-15s $15-s, Name:, Health:,Stamina:,[if([or([t([match(Byakugan Sharingan, [get(%#/dat`TF)])])], [t([match([get(%#/dat`ExpertiseList)], Extra Sensory*, |)])])], Chakra:)])])][iter([u(%l/list`players)], [pemit(%#, [prompt(RP)] [printf($-18s $-25s $-15s $15-s, [ansi([switch([member([u(%l/list`players)], %i0)], [u(%l/player_turn)], hm, n)], [name(%i0)])][if([and([hasattr(%i0, dat`link)], [strmatch(%#, [elements([get(%i0/dat`link)], 3, |)])])], %([ansi(hc, *)]%),)], [u(#590/fn`healthstatus, [fdiv([Get(%i0/DAT`vitality`Current)], [get(%i0/dat`vitality`max)])])], [u(#590/fn`stamstatus, [fdiv([Get(%i0/DAT`STAMINA`CURRENT)], [get(%i0/dat`stamina`max)])])], [if([or([t([match(Byakugan Sharingan, [get(%#/dat`TF)])])], [t([match([get(%#/dat`ExpertiseList)], Extra Sensory*, |)])])], [u(#590/fn`chakrastatus, [get(%i0/dat`Chakra`Current)])])])])])])]




@create Combat Functions=10
@set Combat Functions=INHERIT SIDEFX

&FN`STAMSTATUS #590=[switch(1, [gte(%0, .85)], [ansi(hg, Fine)], [gte(%0, .7)], [ansi(c, Winded)], [gte(%0, .55)], [ansi(y, Tired)], [gte(%0, .4)], [ansi(+orange, Weary)], [gte(%0, .2)], [ansi(r, Fatigued)], [gt(%0, 0)], [ansi(hr, Exhausted)], [ansi(+grey31, Coma)])]

&FN`HEALTHSTATUS #590=[switch(1, [gte(%0, .95)], [ansi(hg, Excellent Condition)], [gte(%0, .75)], [ansi(g, Good Condition)], [gte(%0, .55)], [ansi(y, Average Condition)], [gte(%0, .4)], [ansi(hy, Several Wounds)], [gte(%0, .25)], [ansi(hr, Badly Injured)], [gt(%0, 0)], [ansi(r, Near Death)], [ansi(+grey31, Dead)])]

&FN`CHAKRASTATUS #590=[switch(1, [lte(%0, 1000)], [ansi(0xEE, Drained)], [lte(%0, 2500)], [ansi(0xF3, Minimal)], [lte(%0, 5000)], [ansi(0x34, Insignificant)], [lte(%0, 7500)], [ansi(0xC4, Formidable)], [lte(%0, 10000)], [ansi(0xcA, Empowered)], [lte(%0, 25000)], [ansi(0xd0, Strong)], [lte(%0, 50000)], [ansi(0xd6, Powerful)], [lte(%0, 100000)], [ansi(0xe2, Energized)], [lte(%0, 150000)], [ansi(0xbe, Threatening)], [lte(%0, 200000)], [ansi(0x76, Overwhelming)], [lte(%0, 250000)], [ansi(0x78, Inhuman)], [lte(%0, 300000)], [ansi(0x2E, Bijuu-like)], [lte(%0, 399999)], [ansi(0x31, Sage-like)], [ansi(0x33, God-like)])]

&FN`DOT #590=[if([hasattr(%#, dat`dot)], [if([eq(0, [rest([get(%#/dat`DoT)], |)])], [remit(%l, [prompt(combat)] %N is no longer suffering from Damage Over Time.)][set(%#, dat`dot:)], [remit(%l, [prompt(combat)] %N takes [setr(d, [first([get(%#/dat`DoT)], |)])] in persistent damage!)][set(%#, dat`vitality`current:[sub([get(%#/dat`vitality`current)], %qd)])][set(%#, dat`DoT:%qd|[dec([rest([get(%#/dat`DoT)], |)])])])])]

&FN`WIPE-COMBAT-ATTRIBS #590=[wipe(%0/dat`perceptionCheck)][if([hasattr(%0, dat`copylist)], [iter([get(%0/dat`copylist)], [wipe(%0/dat`jutsu*[edit(##, %b, -)])], |)][wipe(%0/dat`copylist)])][if(hasattr(%0/Dat`TF),[pemit(%0,[prompt(Game)] You have reverted from your [get(%0/Dat`TF)] Transformation.)][set(%q0,Dat`TF:)],)][iter(link exposure control bodymod, [wipe(%0/dat`##)])]

&FN`EXPOSURE #590=[if([hasattr(%#/dat`exposure)], [if([!elements([get(%#/dat`exposure)], 3, |)], [set(%#, dat`exposure:[replace([get(%#/dat`exposure)], 2, [dec([elements([get(%#/dat`exposure)], 2, |)])], |)])] [if([eq(0, [elements([get(%#/dat`exposure)], 2, |)])], [wipe(%#/dat`exposure)])])])]

&FN`BODYMOD #590=[if([hasattr(%#, dat`bodymod)], [set(%#, dat`stamina`current:[sub([get(%#/dat`stamina`current)], 5000)])][pemit(%#, [prompt(combat)] You have lost 5000 stamina from having Body Mod activated.)])]

&FN`ON-ET #590=[iter(dot exposure bodymod, [u(fn`##)])]